# changing directories
setopt AUTO_CD
setopt AUTO_PUSHD
setopt PUSHD_SILENT

# completion
setopt ALWAYS_TO_END
setopt LIST_PACKED

setopt NO_AUTO_REMOVE_SLASH

autoload -U compinit
compinit
zstyle ':completion:*' list-colors ''

# history
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_NO_FUNCTIONS
setopt HIST_NO_STORE
setopt HIST_REDUCE_BLANKS
setopt SHARE_HISTORY

HISTFILE=$HOME/.zsh_history
HISTSIZE=10000
SAVEHIST=10000

# command
setopt CORRECT
setopt NO_FLOW_CONTROL
setopt PATH_DIRS
setopt RM_STAR_WAIT

# zle
setopt NO_BEEP
setopt BRACE_CCL

bindkey -e
bindkey '\CJ' backward-char
bindkey '\ej' backward-word

# source
local aliase_file=${DOTDIR}/shell/aliases
if [[ -f $aliase_file ]]; then
  source $aliase_file
fi

local perlbrew=${PERLBREW_ROOT}/etc/bashrc
if [[ -f $perlbrew ]]; then
  source $perlbrew
fi

# prompt
autoload -U add-zsh-hook

function echo_prompt {
  if [[ -n $RUN_SCREEN ]]; then
    echo -ne "\ek$(print -P '%20<..<%~')\e\\";
  fi;

  prompt='%n@ %30<..<%~$ ';
}
add-zsh-hook precmd echo_prompt

function echo_command {
  if [[ -n $RUN_SCREEN ]]; then
    echo -ne "\\033k$argv[3]\\033\\\\";
  fi;
}
add-zsh-hook preexec echo_command

# git http://d.hatena.ne.jp/mollifier/20100906/p1
autoload -Uz colors
colors
autoload -Uz vcs_info

zstyle ':vcs_info:*' enable git svn hg bzr
zstyle ':vcs_info:*' formats '(%s)-[%b]'
zstyle ':vcs_info:*' actionformats '(%s)-[%b|%a]'
zstyle ':vcs_info:(svn|bzr):*' branchformat '%b:r%r'
zstyle ':vcs_info:bzr:*' use-simple true

autoload -Uz is-at-least
if is-at-least 4.3.10; then
    # この check-for-changes が今回の設定するところ
  zstyle ':vcs_info:git:*' check-for-changes true
  zstyle ':vcs_info:git:*' stagedstr "+"    # 適当な文字列に変更する
  zstyle ':vcs_info:git:*' unstagedstr "-"  # 適当の文字列に変更する
  zstyle ':vcs_info:git:*' formats '(%s)-[%b] %c%u'
  zstyle ':vcs_info:git:*' actionformats '(%s)-[%b|%a] %c%u'
fi

function _update_vcs_info_msg() {
  psvar=()
  LANG=en_US.UTF-8 vcs_info
  [[ -n "$vcs_info_msg_0_" ]] && psvar[1]="$vcs_info_msg_0_"
}

add-zsh-hook precmd _update_vcs_info_msg
RPROMPT="%1(v|%F{green}%1v%f|)"
